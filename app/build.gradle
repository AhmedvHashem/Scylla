apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: '../config/quality.gradle'

def releasePropertiesFile = rootProject.file("private/release.properties")
def releaseProperties = new Properties()
releaseProperties.load(new FileInputStream(releasePropertiesFile))

def currentCommit = 'git rev-parse --short HEAD'.execute().text.trim().subSequence(0, 7)

android {
  compileSdkVersion 29
  defaultConfig {
    applicationId "com.ahmednts.scylla"
    minSdkVersion 21
    targetSdkVersion 29
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  signingConfigs {
    debugConfig {
      storeFile rootProject.file("private/debug.keystore")
      storePassword 'android'
      keyAlias 'androiddebugkey'
      keyPassword 'android'
    }
    releaseConfig {
      storeFile rootProject.file("private/release.keystore")
      storePassword releaseProperties['storePassword']
      keyAlias releaseProperties['keyAlias']
      keyPassword releaseProperties['keyPassword']
    }
  }

  buildTypes {
    debug {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

      debuggable true
      applicationIdSuffix '.debug'
      versionNameSuffix "_${currentCommit}"

      signingConfig signingConfigs.debugConfig
    }
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

      signingConfig signingConfigs.releaseConfig
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = "1.8"
  }
    namespace 'com.ahmednts.scylla'

    lintOptions {
    abortOnError false
    warning 'InvalidPackage'
  }

  applicationVariants.all { variant ->
    variant.outputs.all { output ->
      outputFileName = outputFileName.
          replace(".apk", "_${variant.versionCode}_${variant.versionName}.apk").
          replace(project.name, variant.applicationId.replace("com.ahmednts.scylla.", ""))
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  //Kotlin
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.61"

  //Core
  implementation 'androidx.appcompat:appcompat:1.1.0'
  implementation 'androidx.core:core-ktx:1.2.0'
  implementation 'com.google.android.material:material:1.1.0'

  //arch components
  implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

  //RxJava & RxAndroid
  implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
  implementation 'io.reactivex.rxjava2:rxjava:2.2.14'

  //Utils
  implementation 'com.jakewharton.timber:timber:4.7.1'
  debugImplementation "com.squareup.leakcanary:leakcanary-android:2.0-beta-3"

  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'androidx.test.ext:junit:1.1.1'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
